/ip dhcp-server lease

:if (($leaseBound=1) && ([find where dynamic mac-address=$leaseActMAC]!="")) do={

	# Get the lease number
	:local Lease [find mac-address=$leaseActMAC]
	
	# Get date and time
	:local date [/system clock get date]
	:local time [/system clock get time]
	
	# Make the lease static	
	make-static $Lease

	# Get host name
	:local Name [get $"lease-hostname" ] 
	
	# Add date and time as a comment to show when it was seen first time	
	#comment comment="$date $time $Name" $Lease
	
	# Send a message to the log	
	:log info message="script=dhcp_static server=$leaseServerName IP=$leaseActIP MAC=$leaseActMAC name=$Name"
}

# Set how old data you like to delete.  E.g older than 100 week ->"100w".  Older than 30 days -> "30d"
:local delete "7d"

# Find and delete entries
/ip dhcp-server lease
:foreach id in=[find where dynamic=no last-seen>[:totime $delete]] do={
	:local ip [get $id address]
	:local hostdhcp [get $id host-name]
	:local hostdns "N/A"
	# Remove DNS entry if exist
	:do {
		:set $hostdns [/ip dns static get [find where address=$ip] name]
		/ip dns static remove [find where address=$ip]
	} on-error={ }
	
	# remove DHCP entry
	remove $id
	
	# Send log message
	:log info message="script=dhcp_clean older_than=$delete ip=$ip host_dhcp=\"$hostdhcp\" host_dns=\"$hostdns\""
}