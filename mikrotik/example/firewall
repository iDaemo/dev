# Please note that these rules are using "all-vlan" interface and
# it is assumed that gateway/uplink interface is not using VLAN.
# If gateway/uplink interface uses VLAN, use interface lists instead of
# all-vlan.
# all-vlan example was found from the topic below:
# https://forum.mikrotik.com
# Search for: "Comfortable way to block inter-vlan traffic"
#   Attention, when pasting to serial connection, the whole URL
#   seems to have some strange effects because of the question mark in
#   the URL, even when it's commented out, so the whole URL is not
#   written above.

/ip firewall filter

######## ROUTER FIREWALL RULES ########

# Allow traffic back as long as the traffic has started from "inside"
# your network.
add action=accept chain=input comment="Allow Established & Related" \
    connection-state=established,related
    
# If you're using IPsec, allow untracked.
# This rule is especially important if one is using "Drop all"
# as the last rule.
# More information in forum.mikrotik.com topic:
# "New" default firewall config in ROS - why ipsec is default allowed
add action=accept chain=input \
    comment="Allow untracked" connection-state=untracked disabled=yes
    
# Allow DNS requests from VLANs (udp/tcp)
add action=accept chain=input comment=\
    "Allow DNS requests from LAN / VLANs UDP" dst-port=53 in-interface=\
    all-vlan protocol=udp
add action=accept chain=input comment=\
    "Allow DNS requrests from LAN / VLANs TCP" dst-port=53 in-interface=\
    all-vlan protocol=tcp
    
# Allow pinging the router from the VLANs
# This is mainly a troubleshooting option to check:
# "Why my internet is not working? Well, I can ping the gateway."
add action=accept chain=input comment=\
    "Allow pinging the router  from LAN / VLANs" in-interface=all-vlan \
    protocol=icmp
    
# Allow full access to your router from the Base / Management VLAN
add action=accept chain=input comment="Allow Base Full Access" in-interface=\
    BASE_VLAN

# Allow PING from internet. Might be nice or not.
# It's disabled by default in this conf.
add action=accept chain=input comment="Allow PING from internet" disabled=yes \
    protocol=icmp

# It's important to drop everything else incoming traffic to router,
# so any malicious intends are blocked.
add action=drop chain=input comment="Drop everything else in input"



######## CLIENT/INTERNAL NETWORK FIREWALL RULES ########

# Allow traffic back as long as the traffic has started from "inside"
# client/internal network. 
add action=accept chain=forward comment="Allow Established & Related" \
    connection-state=established,related

# If you're using IPsec, allow untracked.
# This rule is especially important if one is using "Drop all"
# as the last rule.
# More information in forum.mikrotik.com topic:
# "New" default firewall config in ROS - why ipsec is default allowed
add action=accept chain=forward \
    comment="Allow untracked" connection-state=untracked disabled=yes
    
# Allow Base / Management VLAN to connect any other VLAN
add action=accept chain=forward comment=\
    "Allow BASE / mgmt to connect all VLANs" in-interface=BASE_VLAN \
    out-interface=all-vlan

# This rule is associated with the rule below about stopping inter-VLAN
# communication (or drop all). If VLAN communication between certain VLANs
# is required, one can add something like the examples below.
# Notice that one can also only allow one direction of communication.
# In this example communication is allowed in both directions between
# BLUE and GREEN VLANs.
# Notice that the rules are disabled by default.
add action=accept chain=forward \
    comment="Allow communication from BLUE to GREEN VLAN" disabled=yes \
    in-interface=BLUE_VLAN out-interface=GREEN_VLAN
add action=accept chain=forward \
    comment="Allow communication from GREEN to BLUE VLAN" disabled=yes \
    in-interface=GREEN_VLAN out-interface=BLUE_VLAN

# Stop VLANs from communicating each other. By default VLANs are able
# to communicate each other, which doesn't make sense security-wise
# in many scenarios.
# Notice that this doesn't stop VLANs from pinging different
# router/bridge IPs. This doesn't also stop clients inside a certain
# VLAN from communicating each other.
# Notice that this rule is pointless if "Drop all" rule is being used.
add action=drop chain=forward comment="Drop all inter-VLAN traffic" \
    in-interface=all-vlan out-interface=all-vlan

# Drop invalid packets in forward chain is required if one is using NAT.
# Otherwise it's possible that packets leave router with wrong LAN IP address.
# Notice that this rule is pointless if "Drop all" rule is being used.
add action=drop chain=forward comment="Drop invalid in forward chain" \
    connection-state=invalid

# This is a bit of preference of firewall rule creator if "drop all" or
# the "drop invalid" should be used.
# If this rule is used, then the above "drop invalid" and
# "drop inter-vlan communication" are pointless.
# Notice that in this example this is disabled.
add action=drop chain=forward comment="Drop all" disabled=yes